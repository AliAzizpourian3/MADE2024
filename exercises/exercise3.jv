// exercise 3

pipeline FinanceDataPipeline {

    FinanceDataExtractor 
        -> XLSXHandler 
        -> SheetHandler
        -> RangeSelector
        -> HeaderMapper;

    HeaderMapper
        -> GDPInterpreter
        -> GDPDataLoader;

    HeaderMapper
        -> BondInterpreter
        -> BondDataLoader;

    valuetype DecimalRangeZeroToOne oftype decimal {
        constraints: [WithinZeroOne];
    }

    valuetype PositiveDecimalValue oftype decimal {
        constraints: [OnlyPositive];
    }

    constraint WithinZeroOne oftype RangeConstraint {
        lowerBound: 0;
        upperBound: 1;
    }

    constraint OnlyPositive oftype RangeConstraint {
        lowerBound: 0;
    }

    block FinanceDataExtractor oftype HttpExtractor {
        url: 'https://thedocs.worldbank.org/en/doc/7d852628d96b9411d43e5d36d5dff941-0050062022/original/Graphs-Chapter-5-02082022.xlsx';
        retries: 2;
    }

    block XLSXHandler oftype XLSXInterpreter {}

    block SheetHandler oftype SheetPicker {
        sheetName: "Figure S5.1.2";
    }

    block RangeSelector oftype CellRangeSelector {
        select: range P2:S45;
    }

    block HeaderMapper oftype CellWriter {
        at: range A1:D1;
        write: [
            'Country Code',
            'Economy',
            'GDP per Capita',
            'Bond Issuance Share'
        ];
    }

    block GDPInterpreter oftype TableInterpreter {
        header: true;
        columns: [
            'Country Code' oftype CountryCodeAlpha3,
            'GDP per Capita' oftype PositiveDecimalValue,
        ];
    }

    block BondInterpreter oftype TableInterpreter {
        header: true;
        columns: [
            'Country Code' oftype CountryCodeAlpha3,
            'Bond Issuance Share' oftype DecimalRangeZeroToOne,
        ];
    }

    block GDPDataLoader oftype SQLiteLoader {
        table: 'gdpPerCapita';
        file: 'country-stats.sqlite';
    }

    block BondDataLoader oftype SQLiteLoader {
        table: 'bondIssuance';
        file: 'country-stats.sqlite';
    }
}
