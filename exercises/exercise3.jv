valuetype Year oftype integer {
  constraints: [ PositiveInteger ];
}

valuetype Month oftype text {
  constraints: [ GermanMonth ];
}

valuetype GoodsID oftype text {
  constraints: [ GoodsIDPattern ];
}

valuetype Quantity oftype integer {
  constraints: [ PositiveInteger ];
}

constraint PositiveInteger oftype RangeConstraint {
  lowerBound: 1;
  upperBoundInclusive: true;
}

constraint GermanMonth oftype AllowlistConstraint {
  allowlist: [ "Januar", "Februar", "MÃ¤rz", "April", "Mai", "Juni", "Juli", "August", "September", "Oktober", "November", "Dezember" ];
}

constraint GoodsIDPattern oftype RegexConstraint {
  regex: /^NST7-[A-Z0-9]{3}$/;
}

pipeline GoodsTransportedPipeline {
    GoodsTransportedExtractor -> GoodsTransportedTextFileInterpreter
    -> TextLineDeleter
    -> GoodsTransportedCSVInterpreter 
    -> ColumnDeleter
    -> GoodsTransportedTableInterpreter
    -> GoodsTransportedLoader
    ;
    
    block GoodsTransportedExtractor oftype HttpExtractor { 
        url: "https://www-genesis.destatis.de/genesis/downloads/00/tables/46131-0014_00.csv"; 
    }
    
    block GoodsTransportedTextFileInterpreter oftype TextFileInterpreter { 
        encoding: "latin2";  
    }
    
    block TextLineDeleter oftype TextLineDeleter {
        lines: [1, 2, 3, 4, 5, 6, 7, 8, 9];  // Delete the first 9 rows
    }
    
    block GoodsTransportedCSVInterpreter oftype CSVInterpreter { 
        delimiter: ';'; 
    }
    
    block ColumnDeleter oftype ColumnDeleter {
        delete: [ 
            column F, column G, column H, column I, column J, column K, column L, column M, column N, 
            column O, column P, column Q, column R, column S, column T, column U, column V, column W, 
            column X, column Y, column Z, column AA, column AB, column AC, column AD, column AE, column AF, 
            column AG, column AH, column AI, column AJ, column AK, column AL, column AM, column AN, column AO, 
            column AP, column AQ, column AR, column AS
        ]; // Delete all unnecessary columns except A-E and AT-AU
    }

    block GoodsTransportedTableInterpreter oftype TableInterpreter {
        header: false;
        columns: [
            "year" oftype Year,
            "month" oftype Month,
            "goods_id" oftype GoodsID,
            "goods_name" oftype text,
            "goods_source" oftype text,
            "abroad" oftype Quantity,
            "total" oftype Quantity
        ];
    }
    
    block GoodsTransportedLoader oftype SQLiteLoader { 
        table: "goods"; 
        file: "./goodsTransportedByTrain.sqlite"; 
    }
}
